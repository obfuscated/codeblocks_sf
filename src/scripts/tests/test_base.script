
local made_tests = 0;
local passed_tests = 0;
local failed_tests = 0;
local not_implemented_tests = 0;
local sa = wxStopWatch();
local global_made_tests = 0;
local global_passed_tests = 0;
local global_failed_tests = 0;
local global_not_implemented_tests = 0;
local current_teat_name = "none";


function AddGlobalPassed(count)
{
    global_passed_tests += count;
}

function GetGlobalFailedTests()
{
    return global_failed_tests;
}

function GetGlobalNotImplementedTests()
{
	return global_not_implemented_tests;
}

function AddGlobalFailed(count)
{
    global_failed_tests += count;
}

function AddGlobalNotImplemented(count)
{
	global_not_implemented_tests += count;
}

function ResetGlobalTests()
{
    global_made_tests = 0;
    global_passed_tests = 0;
    global_failed_tests = 0;
	global_not_implemented_tests = 0;
}

class script_test_base
{
    function Run()
    {

    }

    function get_padded_string(text,length = 40,fill_char = "=")
    {
        local ret = "";
        ret += "====== " + text + " ";
        local fill = length - ret.len();
        for(local i = 0; i < fill;i++)
            ret += fill_char;
        return ret;
    }

    function begin_test(name)
    {

        print(get_padded_string(name + " BEGINN") + "\n");
        clear_test_result();
        current_teat_name = name;
    }

    function end_test()
    {
        print_test_result();
        print(get_padded_string(current_teat_name + " END") + "\n");
        current_teat_name = "none";
    }

    function format_output(name, result)
    {
        local name_length = name.len();
        local needed_tabs = 0;
        local free_space = 20 - name_length;
        if(free_space < 0)
        {
            free_space = 50 - name_length;
        }
        if(free_space < 0)
            free_space = 4;

        needed_tabs = free_space;
        local output = "Test: " + name;
        local i = 0;
        for(;i<needed_tabs;i++)
        {
            output += " ";
        }
        output += result;
        return output;
    }

    function passed(name)
    {
        //sa.Pause();
        passed_tests++;
        global_passed_tests++;
        print(format_output(name,"PASSED")+ "\n");
        //sa.Resume();
    }

    function made_passed(name)
    {
        global_made_tests++;
        passed(name);
    }

    function failed(name,message)
    {
        //sa.Pause();
        failed_tests++;
        global_failed_tests++;
        error(format_output(name,"FAILED " + message +"\n"));//(got " + to_test + " needed "+result+") \n"));
        //sa.Resume();
    }

    function made_failed(name,message)
    {
        global_made_tests++;
        failed(name,message);
    }

    function clear_test_result()
    {
        made_tests = 0;
        passed_tests = 0;
        failed_tests = 0;
		not_implemented_tests = 0;
        //sa.Start(0);
    }
	
	function not_implemented(name)
	{
		error(format_output(name,"NOT IMPLEMENTED \n"));
		not_implemented_tests++;
		global_not_implemented_tests++;
	}

    function clear_global_test_results()
    {
        global_made_tests = 0;
        global_passed_tests = 0;
        global_failed_tests = 0;
		global_not_implemented_tests= 0;
    }

    function print_global_test_result()
    {
        //sa.Pause();
        print("\n");
        print("Global executed tests:   " + global_made_tests + "\n");
        print("Global passed tests:     " + global_passed_tests + "\n");
        print("Global failed tests:     " + global_failed_tests + "\n");
		print("Global not impl. tests:  " + global_not_implemented_tests + "\n");
        //print("Time elapsed:   " + sa.Time() + "ms ("+ sa.TimeInMicro()+"us)\n");

    }

    function print_test_result()
    {
        //sa.Pause();
        print("\n");
        print("Executed tests:  " + made_tests + "\n");
        print("passed tests:    " + passed_tests + "\n");
        print("failed tests:    " + failed_tests + "\n");
		print("not impl. tests: " + not_implemented_tests + "\n");
        //print("Time elapsed:   " + sa.Time() + "ms ("+ sa.TimeInMicro()+"us)\n");

    }

    function test_equal(name,to_test,result)
    {
        made_tests++;
        global_made_tests++;
        if(to_test == result)
        {
            passed(name);
            return true;
        }
        else
        {
            failed(name,"(got " + to_test + " needed "+result+")");
        }
        return false;
    }

    function test_true(name,to_test)
    {
        made_tests++;
        global_made_tests++;
        if(to_test)
        {
            passed(name);
            return true;
        }
        else
        {
            failed(name,"(got " + to_test + " needed true)");
        }
        return false;
    }

    function test_false(name,to_test)
    {
        made_tests++;
        global_made_tests++;
        if(!to_test)
        {
            passed(name);
            return true;
        }
        else
        {

            failed(name,"(got " + to_test + " needed false)");
        }
        return false;
    }

    function test_type(name,to_test,type)
    {
        made_tests++;
        global_made_tests++;
        if(typeof to_test == typeof type)
        {
            passed(name);
            return true;
        }
        else
        {
            failed(name,"(got " + typeof to_test + " needed " + typeof type + ")");
        }
        return false;
    }
	
	function cmp_string(pa,pb)
	{
		local a = pa.tostring();
		local b = pb.tostring();
		if(a.len() != b.len())
			return -1;
		if(a.len() == 0)
			return 0;
		for(local i=0; i < a.len() ;i++)
		{
			if(a[i] != b[i])
				return -1;
		}
		return 0;
	}

    function test_string(name,to_test,result)
    {
        made_tests++;
        global_made_tests++;

        local tmp_string = "";
        if(typeof to_test == "string")
           tmp_string = to_test;
        else
           tmp_string = to_test.tostring();
		if(cmp_string(tmp_string,result) != 0)
		{
			failed(name, "(got \"" + to_test + "\" needed \"" + result + "\")");
            return false;
		}
        passed(name);
        return true;
    }

};
